MEMORY
{
	/*
	 * top 0x4000 bytes is reserved for the DTB
	 */
	flash   (rx)  : ORIGIN = 0x08000000, LENGTH = 128k
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 16k
	sram1   (rwx) : ORIGIN = 0x20004000, LENGTH = 16k /* malloc */
}

ENTRY(__start)
SECTIONS
{

	. = 0x08000000;

	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.sysinit : {
		__sysinit_start = ABSOLUTE(.);
		*(.sysinit)
		__sysinit_end = ABSOLUTE(.);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram0 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram0
}
